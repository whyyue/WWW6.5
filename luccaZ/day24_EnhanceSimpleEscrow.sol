//SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

/// @title EnhancedSimpleEscrow - A secure escrow contract with timeout, cancellation, and events
contract EnhancedSimpleEscrow {
  enum EscrowState { AWAITING_PAYMENT, AWAITING_DELIVERY, COMPLETE, DISPUTED, CANCELLED }

  address public immutable buyer;
  address public immutable seller;
  address public immutable arbiter;

  uint256 public amount;
  EscrowState public state;
  uint256 public depositTime;
  uint256 public deliveryTimeout; //Duration in seconds after deposit

  event PaymentDeposited(address indexed buyer, uint256 amount);
  event DeliveryConfirmed(address indexed buyer, address indexed seller, uint256 amount);
  event DisputeRaised(address indexed initiator);
  event DisputeResolved(address indexed arbiter, address recipient, uint256 amount);
  event EscrowCancelled(address indexed initiator);
  event DeliveryTimeoutReached(address indexed buyer);

  constructor(address _seller, address _arbiter, uint256 _deliveryTimeout) {
    buyer = msg.sender;
    seller = _seller;
    arbiter = _arbiter;
    state = EscrowState.AWAITING_PAYMENT;
    deliveryTimeout = _deliveryTimeout;
  }

  receive() external payable {
    revert("Direct payments not allowed. Use depositPayment function.");
  }

  function deposit() external payable {
    require(msg.sender == buyer, "Only buyer can deposit");
    require(state == EscrowState.AWAITING_PAYMENT, "Payment already deposited");
    require(msg.value > 0, "Deposit amount must be greater than zero");

    amount = msg.value;
    state = EscrowState.AWAITING_DELIVERY;
    depositTime = block.timestamp;

    emit PaymentDeposited(buyer, amount);
  }

  function confirmDelivery() external {
    require(msg.sender == buyer, "Only buyer can confirm delivery");
    require(state == EscrowState.AWAITING_DELIVERY, "Cannot confirm delivery at this stage");

    state = EscrowState.COMPLETE;
    payable(seller).transfer(amount);

    emit DeliveryConfirmed(buyer, seller, amount);
  }

  function raiseDispute() external {
    require(msg.sender == buyer || msg.sender == seller, "Only buyer or seller can raise a dispute");
    require(state == EscrowState.AWAITING_DELIVERY, "Cannot raise dispute at this stage");

    state = EscrowState.DISPUTED;
    emit DisputeRaised(msg.sender);
  }

  function resolveDispute(bool _releaseToSeller) external {
    require(msg.sender == arbiter, "Only arbiter can resolve disputes");
    require(state == EscrowState.DISPUTED, "No dispute to resolve");

    state = EscrowState.COMPLETE;
    if (_releaseToSeller) {
      payable(seller).transfer(amount);
      emit DisputeResolved(arbiter, seller, amount);
    } else {
      payable(buyer).transfer(amount);
      emit DisputeResolved(arbiter, buyer, amount);
    }
  }

  function cancelAfterTimeout() external {
    require(msg.sender == buyer, "Only buyer can cancel");
    require(state == EscrowState.AWAITING_DELIVERY, "Cannot cancel at this stage");
    require(block.timestamp >= depositTime + deliveryTimeout, "Delivery timeout not reached");

    state = EscrowState.CANCELLED;
    payable(buyer).transfer(address(this).balance);
    emit EscrowCancelled(buyer);
    emit DeliveryTimeoutReached(buyer);
  }

  function cancelMutual() external {
    require(msg.sender == buyer || msg.sender == seller, "Only buyer or seller can cancel");
    require(
      state == EscrowState.AWAITING_PAYMENT || state == EscrowState.AWAITING_DELIVERY,
      "Cannot cancel at this stage"
    );

    EscrowState previousState = state;
    state = EscrowState.CANCELLED;

    if(previousState == EscrowState.AWAITING_DELIVERY) {
      payable(buyer).transfer(address(this).balance);
    }
    emit EscrowCancelled(msg.sender);
  }

  function getTimeLeft() external view returns (uint256) {
    if (state != EscrowState.AWAITING_DELIVERY) {
      return 0;
    }
    if (block.timestamp >= depositTime )  {
      return 0;
    }
    return (depositTime + deliveryTimeout) - block.timestamp;
  }
}