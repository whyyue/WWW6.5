// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/access/Ownable.sol";
import "./day30_MiniDexPair.sol";

contract MiniDexFactory is Ownable {

    bool public whitelistMode = true;

    mapping(address => mapping(address => address)) public getPair;
    address[] public allPairs;

    event PairCreated(address indexed token0, address indexed token1, address pair, uint256 pairIndex);
    event WhitelistModeUpdated(bool enabled);

    constructor(address _initialOwner) Ownable(_initialOwner) {}

    function createPair(address tokenA, address tokenB) external returns (address pair) {
        require(tokenA != tokenB, "Identical tokens");
        require(tokenA != address(0) && tokenB != address(0), "Zero address");

        if (whitelistMode) {
            require(msg.sender == owner(), "Whitelist mode: only owner");
        }

        (address token0, address token1) = tokenA < tokenB ? (tokenA, tokenB) : (tokenB, tokenA);
        require(getPair[token0][token1] == address(0), "Pair already exists");

        MiniDexPair newPair = new MiniDexPair();
        newPair.initialize(token0, token1);

        pair = address(newPair);
        getPair[token0][token1] = pair;
        getPair[token1][token0] = pair;
        allPairs.push(pair);

        emit PairCreated(token0, token1, pair, allPairs.length);
    }

    function allPairsLength() external view returns (uint256) {
        return allPairs.length;
    }

    function setWhitelistMode(bool _enabled) external onlyOwner {
        whitelistMode = _enabled;
        emit WhitelistModeUpdated(_enabled);
    }
}
